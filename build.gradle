subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'checkstyle'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    group = 'me.taylorkelly'
    version = '3.0-SNAPSHOT'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    checkstyle {
        toolVersion = '6.1'
    }

    def checkstyleConfigDir = new File(rootProject.projectDir, 'config/checkstyle')
    checkstyle.configFile = new File(checkstyleConfigDir, 'checkstyle.xml')
    checkstyle.configProperties.checkstyleConfigDir = checkstyleConfigDir

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    javadoc {
        options.setEncoding('UTF-8')
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }


    build.dependsOn(checkstyleMain)
    build.dependsOn(checkstyleTest)
    build.dependsOn(sourcesJar)
    build.dependsOn(javadocJar)

    repositories {
        mavenCentral()
    }

}
