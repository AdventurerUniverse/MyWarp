apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    maven { url "http://repo.bukkit.org/content/groups/public/" }
    maven { url "http://maven.sk89q.com/repo/" }
    maven { url "http://repository-thee140.forge.cloudbees.com/release/" }
    maven { url "http://repository-thee140.forge.cloudbees.com/snapshot/" }
    maven { url "http://repo.mikeprimm.com" }
    maven { url "http://nexus.theyeticave.net/content/repositories/pub_releases" }
}

dependencies {
    compile project(':mywarp-core')
    compile 'org.bukkit:bukkit:1.7.9-R0.2'

    compile 'com.sk89q:squirrelid:0.1.0'
    compile 'de.minehattan:intake:3.1.2-SNAPSHOT'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.0-beta9'

    compile 'net.milkbowl.vault:VaultAPI:1.4'
    compile 'org.anjocaido:essentialsgroupmanager:2.10.1'
    compile 'de.bananaco:bpermissions:2.10.5'
    compile 'us.dynmap:DynmapCoreAPI:1.9.4'
}

processResources {
    filesMatching('**/plugin.yml') {
        expand(projectName: project.projectName,
               version: project.internalVersion,
               projectDescription: project.projectDescription,
               url: project.url
        )
    }
}

// create the default fallback file (for all localizations) from the english localization
task copyLocalizations(type: Copy) {
    def bundleFolder = 'me/taylorkelly/mywarp/lang'
    from('src/main/resources/' + bundleFolder)
    into(new File(sourceSets.main.output.resourcesDir, bundleFolder))
    include('*_en.properties')
    rename { fileName -> fileName.replace('_en', '') }
}

processResources.dependsOn(copyLocalizations)

shadowJar {
    exclude 'LICENSE'

    dependencies {
        include(dependency(':mywarp-core'))
        include(dependency('org.jooq:jooq'))
        include(dependency('org.flywaydb:flyway-core'))
        include(dependency('org.slf4j:slf4j-api'))
        include(dependency('org.apache.logging.log4j:log4j-slf4j-impl'))
        include(dependency('com.sk89q:squirrelid'))
        include(dependency('de.minehattan:intake'))
    }
    relocate('org.jooq', 'me.taylorkelly.mywarp.internal.jooq')
    relocate('org.flywaydb', 'me.taylorkelly.mywarp.internal.flyway')
    relocate('org.slf4j', 'me.taylorkelly.mywarp.internal.slf4j')
    //relocate('org.apache.logging.log4j', 'me.taylorkelly.mywarp.internal.log4j')
    relocate('com.sk89q.squirrelid', 'me.taylorkelly.mywarp.internal.squirrelid')
    relocate('com.sk89q.intake', 'me.taylorkelly.mywarp.internal.intake')
}
build.dependsOn(shadowJar)
