plugins {
    id "com.github.johnrengelman.shadow" version "2.0.4"
    id "ru.vyarus.animalsniffer" version "1.4.5"
}
apply plugin: "ru.vyarus.animalsniffer"

configurations.create("createSignature")

task sig(type: ru.vyarus.gradle.plugin.animalsniffer.signature.BuildSignatureTask) {
    //build new signature as an extension to the JDK's
    signatures configurations.signature
    files configurations.createSignature
}
animalsniffer {
    ignore "org.bukkit.entity.AbstractHorse" //see util.versionsupport.TamedHorseCheck112
    ignore "org.bukkit.Material" //see util.MaterialInfo

    //use the signature generated above
    signatures = sig.outputFiles
    excludeJars "bukkit-*"
    excludeJars "guava-*"

    //the cache cannot be used due to incompatibilities with Java 8,
    //see https://github.com/xvik/gradle-animalsniffer-plugin/wiki/Check-task-performance#known-issues
}

repositories {
    maven { url 'https://raw.github.com/mywarp/repo/master/releases' }
    maven { url 'https://raw.github.com/mywarp/repo/master/snapshots' }
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public/" } //Bukkit API
    maven { url "http://maven.sk89q.com/repo/" } //SquirrelID
    maven { url "https://raw.github.com/rjenkinsjr/maven2/repo" } //SLF4Bukkit
    maven { url "http://repo.mikeprimm.com" } //DynmapAPI
    maven { url "http://nexus.hc.to/content/repositories/pub_releases/" } //Vault
}

dependencies {
    compile project(":mywarp-core")
    compile "org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT"
    compile "com.sk89q:squirrelid:0.1.0"
    compile "net.milkbowl.vault:VaultAPI:1.4"
    compile "org.anjocaido:essentialsgroupmanager:2.10.1"
    compile "de.bananaco:bpermissions:2.10.5"
    compile "us.dynmap:DynmapCoreAPI:1.9.4"

    runtime "info.ronjenkins:slf4bukkit:1.0.0"
    runtime "com.h2database:h2:1.4.+"

    signature "org.codehaus.mojo.signature:java18:1.0@signature" //required to build custom signature
    createSignature "org.bukkit:bukkit:1.7.10-R0.1-SNAPSHOT" //try to stay compatible with the last 'official' version
    createSignature "com.google.guava:guava:10.0.1" //bundled by Bukkit 1.7.10, we should try to stay compatible
}

processResources {
    filesMatching("**/plugin.yml") {
        expand(projectName: project.projectName,
                version: project.internalVersion,
                projectDescription: project.projectDescription,
                url: project.url
        )
    }
}

shadowJar {
    ext.internalPath = "io.github.mywarp.mywarp.internal"

    // exclude various text files from libraries
    exclude "**/META-INF/LICENSE*"
    exclude "**/META-INF/DEPENDENCIES*"
    exclude "**/META-INF/NOTICE*"
    exclude "**/META-INF/README.txt"

    dependencies {
        include(dependency(":mywarp-core"))
        include(dependency("org.jooq:jooq"))
        include(dependency("org.flywaydb:flyway-core"))
        include(dependency("org.slf4j:slf4j-api"))
        include(dependency("info.ronjenkins:slf4bukkit"))
        include(dependency("com.sk89q:squirrelid"))
        include(dependency("com.github.mywarp.intake:intake"))
        include(dependency("com.h2database:h2"))
        include(dependency("com.flowpowered:flow-math"))
    }
    relocate("org.jooq", "${internalPath}.jooq")
    relocate("org.flywaydb", "${internalPath}.flyway")
    relocate("org.h2", "${internalPath}.h2")
    relocate("com.flowpowered", "${internalPath}.flow-math")
    relocate("org.slf4j", "${internalPath}.slf4j")
    relocate("info.ronjenkins", "${internalPath}.slf4bukkit")
    relocate("com.sk89q.intake", "${internalPath}.intake")
    relocate("com.sk89q.squirrelid", "${internalPath}.squirrelid")
}
build.dependsOn(shadowJar)

artifacts {
    archives jar
    archives shadowJar
    archives sourcesJar
    archives javadocJar
}
